//file:noinspection VulnerableLibrariesLocal
plugins {
    id 'java'
    id("io.github.goooler.shadow") version "8.1.8"
    id 'io.freefair.lombok' version '8.6'
    id("io.papermc.paperweight.userdev") version "2.0.0-beta.8"
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'maven-publish'
}

group 'bet.astral'
version '1.2.0'

repositories {
    mavenCentral()
    maven { url "https://repo.papermc.io/repository/maven-public/" }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://jitpack.io' }


    // PAPI
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }

    // Lunar client
    maven {
        name = 'lunarclient'
        url = 'https://repo.lunarclient.dev'
    }

    // World Guard
    maven { url "https://maven.enginehub.org/repo/" }

    // Citizens
    maven {
        name = 'citizens-repo'
        url = 'https://maven.citizensnpcs.co/repo'
    }
}

dependencies {
    implementation 'com.github.AstralLiteratureClub:MoreForJava:1.0.2'
    implementation 'com.github.AstralLiteratureClub:MessageManager:2.4.1'
    implementation 'com.github.AstralLiteratureClub:GUIMan:1.3.1-3'
    implementation 'com.github.AstralLiteratureClub:ChatGameCore:1.0.1'
    implementation 'com.github.AstralLiteratureClub:CloudPlusPlus:1.3.0'

    // LOMBOK
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
    // Armor Change Event
    compileOnly 'com.jeff-media:armor-equip-event:1.0.2'
    // Reflections
    compileOnly 'io.github.classgraph:classgraph:4.8.165'
    // PAPI
    compileOnly 'me.clip:placeholderapi:2.11.5'

    // World Guard
    compileOnly('com.sk89q.worldedit:worldedit-bukkit:7.4.0-SNAPSHOT') {
        exclude group: 'com.google.guava', module: '*'
        exclude group: 'com.google.code.gson', module: '*'
        exclude group: 'org.apache.logging.log4j', module: '*'
    }
    compileOnly('com.sk89q.worldguard:worldguard-bukkit:7.1.0-SNAPSHOT') {
        exclude group: 'com.google.guava', module: '*'
        exclude group: 'com.google.code.gson', module: '*'
        exclude group: 'org.apache.logging.log4j', module: '*'
        exclude group: 'it.unimi.dsi', module: '*'
    }

    // Citizens
    compileOnly('net.citizensnpcs:citizens-main:2.0.35-SNAPSHOT') {
        exclude group: '*', module: '*'
    }
    // Sentinel (Citizens)
    compileOnly('org.mcmonkey:sentinel:2.9.2-SNAPSHOT') {
        exclude group: '*', module: '*'
    }

    var cloudVer = "2.0.0-beta.8"
    implementation 'org.incendo:cloud-paper:'+cloudVer
    implementation 'org.incendo:cloud-brigadier:'+cloudVer
    implementation 'org.incendo:cloud-minecraft-extras:'+cloudVer

    // PAPER MC
    paperweight.paperDevBundle("1.21.4-R0.1-SNAPSHOT")
}

shadowJar {
    archiveClassifier = null
    relocate 'bet.astral.messenger', 'bet.astral.fluffy.libs.messenger'
    relocate 'bet.astral.tuples', 'bet.astral.fluffy.libs.tuples'
    relocate 'bet.astral.shine', 'bet.astral.fluffy.libs.shine'
    relocate 'bet.astral.cloudplusplus', 'bet.astral.fluffy.libs.cloudplusplus'
    relocate 'bet.astral.guiman', 'bet.astral.fluffy.libs.guiman'
}

jar {
    enabled = false
    dependsOn(shadowJar)
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['*.yml']) {
        expand([version: version])
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}

tasks {
    runServer {
        minecraftVersion("1.21.4")
    }
}