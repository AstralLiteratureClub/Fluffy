//file:noinspection VulnerableLibrariesLocal
plugins {
    id 'java'
    // Shadow gradle is broken currently so using googler's one works for now.
    //id 'com.github.johnrengelman.shadow' version '8.1.1'
    id("io.github.goooler.shadow") version "8.1.3"
    //NMS
    id("io.papermc.paperweight.userdev") version "1.5.6"

    id 'maven-publish'

    //LOMBOK
    id 'io.freefair.lombok' version '5.1.0'
}

group 'bet.astral'
version '1.0'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://repo.papermc.io/repository/maven-public/" }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    // MESSAGE MANAGER
    implementation 'bet.astral:Messenger:1.5.1'

    // LOMBOK
    implementation 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    // DISCORD
    compileOnly 'net.dv8tion:JDA:5.0.0-beta.10'

    // GLOWING
    implementation 'io.github.skytasul:glowingentities:1.3.2'

    // Armor Change Event
    implementation 'com.jeff-media:armor-equip-event:1.0.2'


//    compileOnly 'me.clip:placeholderapi:2.11.2'

    implementation 'org.incendo:cloud-core:2.0.0-beta.2'
    implementation 'org.incendo:cloud-paper:2.0.0-beta.2'
    implementation 'org.incendo:cloud-minecraft-extras:2.0.0-beta.2'

    // PAPER MC
    paperweight.paperDevBundle("1.20.2-R0.1-SNAPSHOT")
}



// Some shit is importing 1.13 bukkit api and not 1.20.1 api
configurations.compileOnly {
    exclude group: 'org.spigotmc', module: 'spigot'
    exclude group: 'org.spigotmc', module: 'spigot-api'
    exclude group: 'org.bukkit', module: 'bukkit'
}


shadowJar {
    archiveClassifier = null

    relocate 'bet.astral.messenger', 'bet.astral.fluffy.libs.messenger'
    relocate 'fr.skytasul.glowingentities', 'bet.astral.fluffy.libs.fr.skytasul.glowingentities'
    relocate 'org.incendo.cloud', 'bet.astral.fluffy.libs.org.incendo.cloud'
    relocate 'com.jeff-media', 'bet.astral.fluffy.libs.com.jeff-media'
}

tasks.assemble {
    dependsOn(reobfJar)
}

jar {
    enabled = false
    dependsOn(shadowJar)
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = '17.PREVIEW'
    targetCompatibility = '17.PREVIEW'
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 12 || JavaVersion.current().isJava12Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['*.yml']) {
        expand([version: version])
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
//    repositories {
//        maven {
//            url "http://repo.astral.bet"
//        }
//    }
}